buildscript {
    ext.fasterxml_version = '2.9.5'
    ext.xodus_version = '1.2.2'
}

plugins {
    id "jacoco"
    id "org.jetbrains.kotlin.jvm" version "1.2.40"
    id 'org.springframework.boot' version '1.5.10.RELEASE'
}

apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'

apply from: "$rootDir/gradle/versioning.gradle"

group = 'jalgoarena'
description = """JAlgoArena Problems"""

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

jar {
    baseName = 'jalgoarena-problems'
}

dependencies {
    compile "com.fasterxml.jackson.core:jackson-databind:$fasterxml_version",
            "com.fasterxml.jackson.core:jackson-core:$fasterxml_version",
            "com.fasterxml.jackson.core:jackson-annotations:$fasterxml_version",
            "com.fasterxml.jackson.module:jackson-module-kotlin:$fasterxml_version",
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-actuator",
            'org.springframework.cloud:spring-cloud-starter-eureka',
            "org.jetbrains.kotlin:kotlin-stdlib",
            "org.jetbrains.kotlin:kotlin-reflect",
            "org.jetbrains.xodus:xodus-openAPI:$xodus_version",
            "org.jetbrains.xodus:xodus-entity-store:$xodus_version"

    testCompile 'junit:junit:4.12',
            "com.winterbe:expekt:0.5.0",
            "org.springframework.boot:spring-boot-starter-test"
}

apply plugin: 'jacoco'

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

defaultTasks 'clean', 'bootRepackage'

task stage {
    dependsOn 'bootRepackage'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Edgware.SR3"
    }
}

task releaseZip(type: Zip, dependsOn: 'bootRepackage') {
    from('build/libs/') {
        include('*.jar')
    }
    from 'run.sh'
    from('ProblemsStore') {
        into('ProblemsStore')
        exclude('*.lck')
    }
    archiveName "JAlgoArena-Problems-${version}.zip"
    destinationDir(file('build/libs'))
}
